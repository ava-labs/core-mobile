// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        appVersion = [
            major: 0,
            minor: 0,
            patch: 0
        ]
        appBuildNumber = 1
        appVersionName = "${appVersion.major}.${appVersion.minor}.${appVersion.patch}"
        buildToolsVersion = "35.0.0"
        minSdkVersion = 28
        compileSdkVersion = 35
        targetSdkVersion = 35
        androidXAnnotation = "1.2.0"
        androidXBrowser = "1.3.0"
        kotlinVersion = "2.0.21"
        ndkVersion = "27.1.12297006"
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath("com.android.tools.build:gradle")
        classpath("com.facebook.react:react-native-gradle-plugin")
        classpath('org.jetbrains.kotlin:kotlin-gradle-plugin')
        classpath 'com.google.gms:google-services:4.4.2'
    }
    subprojects { subproject ->
        afterEvaluate{
            if((subproject.plugins.hasPlugin('android') || subproject.plugins.hasPlugin('android-library'))) {
                android {
                    compileSdkVersion rootProject.ext.compileSdkVersion
                    buildToolsVersion rootProject.ext.buildToolsVersion
                }
            }
        }
    }
}


def reactNativeAndroidDir = new File(
  providers.exec {
    workingDir(rootDir)
    commandLine("node", "--print", "require.resolve('react-native/package.json')")
  }.standardOutput.asText.get().trim(),
  "../android"
)

allprojects {
    repositories {
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url(reactNativeAndroidDir)
        }

        google() // TODO: Verify if this is needed
        maven { url("$rootDir/../node_modules/detox/Detox-android") } // This is required for detox to work
        mavenCentral()
        maven { url "$rootDir/../node_modules/expo-camera/android/maven" }
        maven { url 'https://www.jitpack.io' }
    }
    
    configurations.all {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            if (details.requested.group == 'org.jetbrains.kotlin') {
                details.useVersion "2.0.21"
            }
        }
    }
}

project.ext {
    excludeAppGlideModule = true
    glideVersion = "4.9.0"
}


apply plugin: "expo-root-project"
apply plugin: "com.facebook.react.rootproject"